#version 450 core

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

/*** HEADER ***/

void main() {

	dvec2 nodePos = dvec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);
	uvec2 node = uvec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y); // grid node index

	uint N = points.length();

	double nodeMass = 0.0;
	dvec2 nodeMomentum = dvec2(0.0);
	dvec2 nodeForce = dvec2(0.0);
	
	// P2G
	for (uint p = 0; p < N; ++p) {
		dvec2 dpg = nodePos - points[p].x;
		double dx = -dpg.x; // sign matters for gradient
		double dy = -dpg.y;
		double wpg = BSpline(dx) * BSpline(dy);
		dvec2 wpgGrad = dvec2(BSplineSlope(dx) * BSpline(dy),
							BSpline(dx) * BSplineSlope(dy));

		nodeMass += wpg * points[p].m;
		nodeMomentum += wpg * points[p].m * (points[p].v + points[p].B * Dp_inv * dpg);
		nodeForce -= points[p].vol * points[p].P * transpose(points[p].F) * wpgGrad;
	}

	// Final steps of P2G
	nodes[node.x][node.y].m += nodeMass;
	nodes[node.x][node.y].momentum += nodeMomentum;
	nodes[node.x][node.y].force += nodeForce;
}